plugins {
    id "com.android.application"
    id "kotlin-android"
    // The Flutter Gradle Plugin must be applied after the Android and Kotlin Gradle plugins.
    id "dev.flutter.flutter-gradle-plugin"
}

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
} else {
    // Set default empty values for CI/CD environments
    keystoreProperties['keyAlias'] = System.getenv('KEY_ALIAS') ?: ''
    keystoreProperties['keyPassword'] = System.getenv('KEY_PASSWORD') ?: ''
    keystoreProperties['storeFile'] = System.getenv('KEYSTORE_FILE') ?: 'rel.jks'
    keystoreProperties['storePassword'] = System.getenv('STORE_PASSWORD') ?: ''
}

android {
    namespace "com.nekkochan.mangareader"
    compileSdkVersion 36
    ndkVersion flutter.ndkVersion

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    defaultConfig {
        // TODO: Specify your own unique Application ID (https://developer.android.com/studio/build/application-id.html).
        applicationId "com.nekkochan.mangareader"
        // You can update the following values to match your application needs.
        // For more information, see: https://flutter.dev/to/review-gradle-config.
        minSdkVersion flutter.minSdkVersion
        targetSdkVersion 36
        versionCode 1
        versionName "1.0"
    }

    signingConfigs {
        release {
            if (keystoreProperties['keyAlias']) {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile keystoreProperties['storeFile'] ? file(keystoreProperties['storeFile']) : null
                storePassword keystoreProperties['storePassword']
            }
        }
    }

    buildTypes {
        release {
            // Only apply signing config if keystore properties are available
            if (keystoreProperties['keyAlias']) {
                signingConfig signingConfigs.release
            }
            minifyEnabled false
            shrinkResources false
        }
        debug {
            minifyEnabled false
        }
    }
}

flutter {
    source = "../.."
}
